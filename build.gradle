buildscript {

  ext {
    springBootVersion = '2.2.5.RELEASE'
    guavaVersion = '28.1-jre'
    groovyVersion = '2.5.9'
    lombokVersion = '1.18.10'
    artifactoryConf = [
      url : System.getenv("ARTIFACTORY_URL") ?: "http://repo.myorg.com/artifactory/libs-releases",
      username : System.getenv("ARTIFACTORY_USERNAME") ?: "resolver",
      password : System.getenv("ARTIFACTORY_PASSWORD") ?: "resolverPaS*",
      repo : System.getenv("ARTIFACTORY_REPOSITORY") ?: "libs-releases"
    ]
  }

  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath('org.jfrog.buildinfo:build-info-extractor-gradle:latest.release')
  }

}

version = '1.0.0-SNAPSHOT'
group = 'org.hidetake.stubyaml'

subprojects {

  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'com.jfrog.artifactory'

  group = parent.group
  version = parent.version

  sourceCompatibility = '11'

  compileJava {
    options.compilerArgs = [
      "--release", "8"
    ]
  }

  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
      url 'https://jitpack.io'
    }
  }

  dependencies {
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion" 
    annotationProcessor('com.github.bsideup.jabel:jabel-javac-plugin:0.2.0') {
      exclude group: 'net.bytebuddy', module: 'byte-buddy'
      exclude group: 'net.bytebuddy', module: 'byte-buddy-agent'
    }

    annotationProcessor 'net.bytebuddy:byte-buddy:1.10.8'
    annotationProcessor 'net.bytebuddy:byte-buddy-agent:1.10.8'
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  if(project.name != 'app') {
    publishing {
      publications {
        mavenJava(MavenPublication) {
          from components.java
          artifact (sourcesJar) {
            classifier = 'sources'
          }
        }
      }
    }
  }

  artifactory {
    publish {
      contextUrl = artifactoryConf.url
      repository {
        repoKey = artifactoryConf.repo
        username = artifactoryConf.username
        password = artifactoryConf.password
      }
      defaults {
        publications('mavenJava')
      }
    }
  }

}